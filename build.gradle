plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false

    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0" // literally never gonna update
}

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven { url = "https://maven.resourcefulbees.com/repository/maven-public/" }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release = 17
        }
    }

    java {
        withSourcesJar()
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
//            parchment("org.parchmentmc.data:parchment-$rootProject.minecraft_version:$rootProject.parchment_version@zip")
        }

        modApi "com.teamresourceful.resourcefullib:resourcefullib-$name-$minecraft_version:$resourcefullib_version"
        modApi "com.teamresourceful.resourcefulconfig:resourcefulconfig-$name-$minecraft_version:$resourcefulconfig_version"
    }

    var env = System.getenv()
    var modLoader = name

    var changelogFile = rootProject.file("changelog.txt")
    var changelogText = changelogFile.exists() ? changelogFile.text : ""

    if (env.MODRINTH_TOKEN) {
        modrinth {
            projectId = modrinth_id
            uploadFile = tasks.remapJar
            gameVersions = [minecraft_version]
            loaders = [modLoader]
            changelog = changelogText

            if (modLoader == "fabric") {
                dependencies {
                    required.project "fabric-api"
                    required.project "resourceful-lib"
                    required.project "resourceful-config"
                    optional.project "modmenu"
                    optional.project "rei"
                }
            } else {
                dependencies {
                    required.project "resourceful-lib"
                    required.project "resourceful-config"
                    optional.project "rei"
                }
            }
        }
    }

    if (env.CURSE_TOKEN) {
        curseforge {
            apiKey = env.CURSE_TOKEN
            project {
                id = curse_id
                changelog = changelogText
                releaseType = 'release'
                addGameVersion minecraft_version
                addGameVersion modLoader.capitalize()

                mainArtifact(tasks.remapJar)

                if (modLoader == "fabric") {
                    relations {
                        requiredDependency "fabric-api"
                        requiredDependency "resourceful-lib"
                        requiredDependency "resourceful-config"
                        optionalDependency "modmenu"
                        optionalDependency "jei"
                        optionalDependency "roughly-enough-items"
                    }
                } else {
                    relations {
                        requiredDependency "resourceful-lib"
                        requiredDependency "resourceful-config"
                        optionalDependency "jei"
                        optionalDependency "roughly-enough-items"
                    }
                }
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact remapJar
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }

                artifactId = "$rootProject.mod_id-$modLoader-$rootProject.minecraft_version"

                pom {
                    name = github_name
                    description = ''
                    url = "https://github.com/AlexNijjar/$github_name"

                    scm {
                        connection = "git:https://github.com/AlexNijjar/${github_name}.git"
                        developerConnection = "git:https://github.com/AlexNijjar/${github_name}.git"
                        url = "https://github.com/AlexNijjar/$github_name"
                    }

                    licenses {
                        license {
                            name = 'MIT'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = "https://maven.resourcefulbees.com/repository/alexnijjar/"
                credentials {
                    username env.MAVEN_USER
                    password env.MAVEN_PASS
                }
            }
        }
    }
}